start: statement+

statement: iface_def_stmt SEP
           | include_stmt SEP

iface_def_stmt: "runtime_class" class_name "{" class_stmt* "}"

class_name: IDENT
class_stmt: client_header_stmt SEP
            | client_name_stmt SEP
            | attribute_stmt SEP
            | property_stmt SEP

/////

client_header_stmt: "client_hdr" ESCAPED_STRING
client_name_stmt: "client_name" CXX_NAME
attribute_stmt: ("scriptable" | "abstract" | "smartptr" | "cloneable")

/////

property_stmt: "property" TYPE_NAME IDENT prop_redirect_modifier?
prop_redirect_modifier: "=>" "redirect" "(" getter_name "," setter_name ")"
getter_name: IDENT
setter_name: IDENT

/////

include_stmt: "import" "\"" FILE_PATH "\""

/////

FILE_PATH: /[_0-9a-zA-Z\/\.]+/
IDENT: CNAME
SEP: ";"
CXX_NAME: CNAME ("::" CNAME)*
TYPE_NAME: ("string" | "real" | "integer" | "boolean")

COMMENT: "//" /[^\n]*/ NEWLINE

%import common.CNAME
%import common.ESCAPED_STRING
%import common.NEWLINE
%import common.SIGNED_NUMBER
%import common.WS_INLINE
%import common.WS
%ignore WS
%ignore COMMENT
